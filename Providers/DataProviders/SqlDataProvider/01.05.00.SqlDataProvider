CREATE TABLE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Rating (
	[RatingID] [int] IDENTITY (1, 1) NOT NULL ,
	[PropertyID] [int] NOT NULL ,
	[UserID] [int] NOT NULL ,
	[CommentID] [int] NULL ,
	[Rating] [float] NOT NULL ,
	[CreateDate] [datetime] NOT NULL 
) ON [PRIMARY]
GO

ALTER TABLE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Rating ADD 
	CONSTRAINT [DF_{objectQualifier}Ventrian_PropertyAgent_Rating_CreateDate] DEFAULT (getdate()) FOR [CreateDate],
	CONSTRAINT [PK_{objectQualifier}Ventrian_PropertyAgent_Rating] PRIMARY KEY  CLUSTERED 
	(
		[RatingID]
	)  ON [PRIMARY] 
GO

CREATE TABLE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Comment (
	[CommentID] [int] IDENTITY (1, 1) NOT NULL ,
	[PropertyID] [int] NOT NULL ,
	[UserID] [int] NULL ,
	[Comment] [ntext] NOT NULL ,
	[CreateDate] [datetime] NOT NULL ,
	[Name] [nvarchar] (255) NULL ,
	[Email] [nvarchar] (255) NULL ,
	[Website] [nvarchar] (255) NULL 
) ON [PRIMARY] TEXTIMAGE_ON [PRIMARY]
GO

ALTER TABLE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Comment ADD 
	CONSTRAINT [PK_{objectQualifier}Ventrian_PropertyAgent_Comment] PRIMARY KEY  CLUSTERED 
	(
		[CommentID]
	)  ON [PRIMARY] 
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_CommentAdd
	@PropertyID int, 
	@UserID int,
	@Comment ntext, 
	@CreateDate datetime,
	@Name nvarchar(255),
	@Email nvarchar(255),
	@Website nvarchar(255) 
AS

INSERT INTO {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Comment (
	[PropertyID], 
	[UserID],
	[Comment],
	[CreateDate],
	[Name],
	[Email],
	[Website]
) VALUES (
	@PropertyID, 
	@UserID,
	@Comment,
	@CreateDate,
	@Name,
	@Email,
	@Website
)

select SCOPE_IDENTITY()
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_CommentList
	@PropertyID int
AS

SELECT     
	c.CommentID,
	c.PropertyID,
	c.UserID,
	c.Comment,
	c.CreateDate,
	CASE c.UserID
        	WHEN -1 THEN c.Email
         	ELSE u.Email
      	END as 'Email',
	CASE c.UserID
        	WHEN -1 THEN c.Name
         	ELSE u.DisplayName
      	END as 'Name',
	c.Website,
	u.Username
FROM         
	{databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Comment c LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users u ON c.UserID = u.UserID
WHERE
	c.PropertyID = @PropertyID
ORDER BY
	c.CreateDate ASC
GO

DROP PROCEDURE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_PropertyList
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_PropertyList
	@ModuleID int, 
	@PropertyTypeID int,
	@Status int, 
	@AuthorID int, 
	@IsActive bit,
	@IsPending bit,
	@IsExpired bit,
	@ShowFeaturedOnly bit,
	@SortBy int,
	@SortByID int,
	@SortByIDType int,
	@SortOrder int,
	@CustomFieldIDs nvarchar(4000),
	@SearchValues	nvarchar(4000),
	@PageNumber int, 
	@PageSize int,
	@BubbleFeatured bit,
	@SearchSubTypes bit
AS

CREATE TABLE #TempItems 
(
   ID int IDENTITY,
   PropertyID int
)

IF( @PropertyTypeID is not null and @SearchSubTypes = 1 )
BEGIN
	DECLARE @current int
	SELECT @current = @PropertyTypeID
	DECLARE @level int, @line int
 
	CREATE TABLE #hierarchy(HierarchyID int IDENTITY (1,1), PropertyTypeID int, level int)
	CREATE TABLE #stack (item int, level int)
	INSERT INTO #stack VALUES (@current, 1)
	SELECT @level = 1
 
	WHILE @level > 0
	BEGIN
   		IF EXISTS (SELECT * FROM #stack WHERE level = @level)
      		BEGIN
         			SELECT @current = item
         			FROM #stack
         			WHERE level = @level
 
 			insert into #hierarchy(PropertyTypeID, level) values(@current, @level) 
 
         			DELETE FROM #stack
         			WHERE level = @level
            			AND item = @current
 
         			INSERT #stack
            			SELECT PropertyTypeID, @level + 1
            			FROM {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_PropertyType
            			WHERE ParentID = @current and ModuleID = @ModuleID 

         			IF @@ROWCOUNT > 0
            				SELECT @level = @level + 1
      			END
   		ELSE
     	 		SELECT @level = @level - 1
	END -- WHILE
END

IF (@CustomFieldIDs is not null AND @SearchValues is not null)
BEGIN
	CREATE TABLE #SearchItems 
	(
   		PropertyID int
	)

	INSERT INTO #SearchItems select PropertyID from {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_CustomFieldSearch(@CustomFieldIDs, @SearchValues)
END
 
DECLARE @maxRow int
DECLARE @startRowIndex int

SET @startRowIndex = (@PageNumber * @PageSize)

SET @maxRow = (@startRowIndex + @PageSize)
SET ROWCOUNT @maxRow

DECLARE @strFields	nvarchar(2000)
DECLARE @strFrom	nvarchar(2000)
DECLARE @strWhere	nvarchar(2000)
DECLARE @strOrder	nvarchar(2000)

SELECT @strFields = 'SELECT p.[PropertyID] '
SELECT @strFrom = ' FROM {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Property p'
SELECT @strWhere = ' WHERE p.[ModuleID] = ' + convert(nvarchar, @ModuleId)
SELECT @strOrder = ' ORDER BY'

IF (@PropertyTypeID is not null and @SearchSubTypes = 1)
	SELECT @strWhere = @strWhere + ' AND p.PropertyTypeID in (select PropertyTypeID from #hierarchy)'

IF (@PropertyTypeID is not null and @SearchSubTypes = 0)
	SELECT @strWhere = @strWhere + ' AND p.PropertyTypeID = ' + convert(nvarchar, @PropertyTypeID)

IF (@Status is not null)
	SELECT @strWhere = @strWhere + ' AND p.Status = ' + convert(nvarchar, @Status)

IF (@AuthorID is not null)
	SELECT @strWhere = @strWhere + ' AND p.AuthorID = ' + convert(nvarchar, @AuthorID)

IF (@IsActive is not null)
	SELECT @strWhere = @strWhere + ' AND (p.[DatePublished] < GetDate() and (p.[DateExpired] is null or p.[DateExpired] > GetDate()))'

IF (@IsPending is not null)
	SELECT @strWhere = @strWhere + ' AND (p.[DatePublished] > GetDate())'

IF (@IsExpired is not null)
	SELECT @strWhere = @strWhere + ' AND (p.[DateExpired] is not null and p.[DateExpired] < GetDate())'

IF (@ShowFeaturedOnly is not null)
	SELECT @strWhere = @strWhere + ' AND p.IsFeatured = 1'

IF (@CustomFieldIDs is not null AND @SearchValues is not null)
	SELECT @strWhere = @strWhere + ' AND (p.PropertyID in (select PropertyID from #SearchItems))'

IF (@BubbleFeatured = 1)
	SELECT @strOrder = @strOrder + ' p.[IsFeatured] DESC' 

DECLARE @strOrder2 nvarchar(2000)	

IF (@SortBy = 3) 
BEGIN
	SELECT @strFrom = @strFrom + ' LEFT JOIN {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_PropertyValue pv ON p.PropertyID = pv.PropertyID'
	SELECT @strWhere = @strWhere + ' AND (pv.[CustomFieldID] is null OR pv.[CustomFieldID] = ' + convert(nvarchar, @SortByID) + ')'
	
	SELECT @strOrder2 = ''	
		
	IF( @SortOrder = 1 and @SortByIDType is null)
	BEGIN
		SELECT @strOrder2 = ' cast(pv.CustomValue as nvarchar) DESC'
	END
		
	IF( @SortOrder = 0 and @SortByIDType is null)
	BEGIN
		SELECT @strOrder2 = ' cast(pv.CustomValue as nvarchar) ASC'
	END
		
	IF( @SortOrder = 1 and (@SortByIDType <> 1 and @SortByIDType <> 3 and @SortByIDType <> 4) )
	BEGIN
		SELECT @strOrder2 = ' cast(pv.CustomValue as nvarchar) DESC'
	END

	IF( @SortOrder = 0 and (@SortByIDType <> 1 and @SortByIDType <> 3 and @SortByIDType <> 4) )
	BEGIN
		SELECT @strOrder2 = ' cast(pv.CustomValue as nvarchar) ASC'
	END

	IF( @SortOrder = 1 and (@SortByIDType = 1 or @SortByIDType = 3 or @SortByIDType = 4) )
	BEGIN
		SELECT @strOrder2 = ' cast(cast(pv.CustomValue as nvarchar) as decimal) DESC'
	END

	IF( @SortOrder = 0 and (@SortByIDType = 1 or @SortByIDType = 3 or @SortByIDType = 4) )
	BEGIN
		SELECT @strOrder2 = ' cast(cast(pv.CustomValue as nvarchar) as decimal) ASC'
	END

	IF( @strOrder = ' ORDER BY' ) 
	BEGIN
		SELECT @strOrder = @strOrder + @strOrder2
	END
	ELSE
	BEGIN
		SELECT @strOrder = @strOrder + ',' + @strOrder2
	END
END
ELSE
BEGIN
	SELECT @strOrder2 = ''	

	IF( @SortBy = 0 and @SortOrder = 1 )
	BEGIN
		SELECT @strOrder2 = ' convert(varchar, p.[DateModified], 120) DESC'
	END

	IF( @SortBy = 0 and @SortOrder = 0 )
	BEGIN
		SELECT @strOrder2 = ' convert(varchar, p.[DateModified], 120) ASC'
	END

	IF( @SortBy = 1 and @SortOrder = 1 )
	BEGIN
		SELECT @strOrder2 = ' convert(varchar, p.[DatePublished], 120) DESC'
	END

	IF( @SortBy = 1 and @SortOrder = 0 )
	BEGIN
		SELECT @strOrder2 = ' convert(varchar, p.[DatePublished], 120) ASC'
	END



	IF( @SortBy = 2 and @SortOrder = 1 )
	BEGIN
		SELECT @strOrder2 = ' p.[ViewCount] DESC'
	END

	IF( @SortBy = 2 and @SortOrder = 0 )
	BEGIN
		SELECT @strOrder2 = ' p.[ViewCount] ASC'
	END

	IF( @SortBy = 4 )
	BEGIN
		SELECT @strOrder2 = ' CAST((rand(24701) * p.[PropertyID]) * 24701 AS int) % (DATEPART(ms, GetDate())+1) + DATEPART(ms, GetDate())'
	END

	IF( @SortBy = 5 )
	BEGIN
		SELECT @strFrom = @strFrom + ' LEFT JOIN {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_PropertyType pt ON p.PropertyTypeID = pt.PropertyTypeID'
		IF( @SortOrder = 1 )
		BEGIN
			SELECT @strOrder2 = ' pt.[Name] DESC'
		END

		IF( @SortOrder = 0 )
		BEGIN
			SELECT @strOrder2 = ' pt.[Name] ASC'
		END
	END

	IF( @strOrder = ' ORDER BY' ) 
	BEGIN
		SELECT @strOrder = @strOrder + @strOrder2
	END
	ELSE
	BEGIN
		SELECT @strOrder = @strOrder + ',' + @strOrder2
	END
END

PRINT('INSERT INTO #TempItems (PropertyID) ' + @strFields + @strFrom + @strWhere + @strOrder)
EXEC('INSERT INTO #TempItems (PropertyID) ' + @strFields + @strFrom + @strWhere + @strOrder)

if( @PageSize > 0 )
	SET ROWCOUNT @PageSize

SELECT
	p.[PropertyID],
	p.[ModuleID],
	p.[PropertyTypeID],
	p.[IsFeatured],
	p.[DateCreated],
	p.[DateModified],
	p.[DatePublished],
	p.[DateExpired],
	p.[ViewCount],
	p.[Status], 
	p.[AuthorID], 
	pt.[Name] as 'PropertyTypeName',
	pt.[Description] as 'PropertyTypeDescription',
	(select count(*) from {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Photo photo where p.PropertyID = photo.PropertyID) as 'PhotoCount',
	(select top 1 [PhotoID] from {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Photo photo where p.PropertyID = photo.PropertyID order by SortOrder) as 'FirstPhotoID',
	(select avg(rating) from {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Rating rating where p.PropertyID = rating.PropertyID) as 'Rating',
	(select count(*) from {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Comment comment where p.PropertyID = comment.PropertyID) as 'CommentCount',
	u.[UserName],
	u.[FirstName] + ' ' + u.[LastName] as 'FullName',
	u.[Email]  
FROM #TempItems t
   	INNER JOIN {databaseOwner}{objectQualifier}ventrian_propertyagent_property p ON
       		t.PropertyID = p.PropertyID
	LEFT JOIN {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_PropertyType pt 
		ON p.PropertyTypeID = pt.PropertyTypeID
	LEFT JOIN {databaseOwner}{objectQualifier}Users u
		ON p.AuthorID = u.UserID 
WHERE
	ID > @startRowIndex
DROP TABLE #TempItems

SET ROWCOUNT 0

SELECT @strFields = 'SELECT Count(*) as ''TotalRecords'' '
EXEC(@strFields + @strFrom + @strWhere)

IF (@CustomFieldIDs is not null AND @SearchValues is not null)
BEGIN
	DROP TABLE #SearchItems 
END

IF (@PropertyTypeID is not null)
BEGIN
	drop table #stack
	drop table #hierarchy
END
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_RatingAdd
	@PropertyID int, 
	@UserID int,
	@CommentID int,
	@Rating float,
	@CreateDate datetime
AS

INSERT INTO {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Rating (
	[PropertyID], 
	[UserID],
	[CommentID],
	[Rating],
	[CreateDate]
) VALUES (
	@PropertyID, 
	@UserID,
	@CommentID,
	@Rating,
	@CreateDate
)

select SCOPE_IDENTITY()
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_RatingGet
	@PropertyID int,
	@UserID int 
AS

SELECT     
	RatingID, 
	PropertyID, 
	UserID, 
	CommentID, 
	Rating, 
	CreateDate
FROM         
	{databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Rating
WHERE
	PropertyID = @PropertyID
	and
	UserID = @UserID
GO

DROP PROCEDURE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_PropertyGet
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_PropertyGet
	@PropertyID int
AS

SELECT
	p.[PropertyID],
	p.[ModuleID],
	p.[PropertyTypeID],
	p.[IsFeatured],
	p.[DateCreated],
	p.[DateModified],
	p.[DatePublished],
	p.[DateExpired],
	p.[ViewCount],
	p.[Status], 
	p.[AuthorID], 
	pt.[Name] as 'PropertyTypeName',
	pt.[Description] as 'PropertyTypeDescription',
	(select count(*) from {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Photo photo where p.PropertyID = photo.PropertyID) as 'PhotoCount',
	(select top 1 [PhotoID] from {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Photo photo where p.PropertyID = photo.PropertyID order by SortOrder) as 'FirstPhotoID',
	(select avg(rating) from {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Rating rating where p.PropertyID = rating.PropertyID) as 'Rating',
	(select count(*) from {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Comment comment where p.PropertyID = comment.PropertyID) as 'CommentCount',
	u.[UserName],
	u.[FirstName] + ' ' + u.[LastName] as 'FullName',
	u.[Email]   
FROM 
	{databaseOwner}{objectQualifier}Ventrian_PropertyAgent_Property p
		LEFT JOIN {databaseOwner}{objectQualifier}Ventrian_PropertyAgent_PropertyType pt 
			ON p.PropertyTypeID = pt.PropertyTypeID
		LEFT JOIN {databaseOwner}{objectQualifier}Users u
			ON p.AuthorID = u.UserID 
WHERE
	[PropertyID] = @PropertyID
GO
